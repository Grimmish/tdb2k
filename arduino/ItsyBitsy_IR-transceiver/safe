/*
* Go Faster-Faster (GF2) headunit
*/
#include <SPI.h>
#include <nRF24L01.h>
#include <RF24.h>
#include <Wire.h>
#include <pins_arduino.h>

RF24 radio(3, 2); // CE, CSN

#define BATTSENSE A0  // https://www.arduino.cc/en/Tutorial/AnalogInput
#define PIEZO1 A1
#define PIEZO2 A3
#define IRSENSE 7
#define LED1 13
#define LED2 12
#define LED3 11
#define LED4 10
#define LED5 9

// Utilize buzzer and LEDs at startup to aid testing, then
// go dark & silent to conserve battery power. This mode is
// active for TESTMODE_MS milliseconds.
#define TESTMODE_MS 120000

unsigned long lastVisiblePulse = 0;
int lastState;
bool beamBroken = false;
bool saturated = false;

void piezoBeep(int duration) {
  for (int i=0; i<duration*5; i++) {
    digitalWrite(PIEZO1, LOW);
    digitalWrite(PIEZO2, HIGH);
    delayMicroseconds(100);
    digitalWrite(PIEZO1, HIGH);
    digitalWrite(PIEZO2, LOW);
    delayMicroseconds(100);
  }
}

void showBatteryLevel() {
  int battvoltage = analogRead(BATTSENSE);

  /* Normal scale for analogRead means max reading (1023) == 3.3 VDC.
     But we're reading through a 50% resistor matrix, so 1023 == 6.6 VDC.
     Therefore for a lithium-ion cell, our "acceptable" range is:
        574 == 3.7 VDC
        652 == 4.2 VDC  */

  if ((battvoltage < 543) || (battvoltage > 667)) {  // Outside normal params
    for (int i=0; i<3; i++) {
      digitalWrite(LED2, HIGH);
      digitalWrite(LED4, HIGH);
      piezoBeep(400);
      digitalWrite(LED2, LOW);
      digitalWrite(LED4, LOW);
      delay(400);
    }
  }
  else if (battvoltage < 574) { // < 3.7 VDC: Crit low
    for (int i=0; i<3; i++) {
      digitalWrite(LED1, HIGH);
      piezoBeep(400);
      digitalWrite(LED1, LOW);
      delay(400);
    }
  }
  else { // 3.7 - 4.3 VDC: Normal
    digitalWrite(LED1, HIGH);                        // >= 3.7 VDC
    if (battvoltage > 590) digitalWrite(LED2, HIGH); // >= 3.8 VDC
    if (battvoltage > 606) digitalWrite(LED3, HIGH); // >= 3.9 VDC
    if (battvoltage > 622) digitalWrite(LED4, HIGH); // >= 4.0 VDC
    if (battvoltage > 638) digitalWrite(LED5, HIGH); // >= 4.1 VDC
  }
  delay(1000);
}

void setup() {
  radio.begin();
  radio.openWritingPipe(0xE0E0E0E0E0);
  // Library docs recommend using a different pipe for RX
  radio.openReadingPipe(1, 0xE1E1E1E1E2);
  radio.setPALevel(RF24_PA_MAX);
  radio.enableDynamicPayloads();
  radio.stopListening();

  pinMode(LED1, OUTPUT); digitalWrite(LED1, LOW);
  pinMode(LED2, OUTPUT); digitalWrite(LED2, LOW);
  pinMode(LED3, OUTPUT); digitalWrite(LED3, LOW);
  pinMode(LED4, OUTPUT); digitalWrite(LED4, LOW);
  pinMode(LED5, OUTPUT); digitalWrite(LED5, LOW);
  pinMode(IRSENSE, INPUT_PULLUP);
  digitalWrite(IRSENSE, HIGH);
  pinMode(PIEZO1, OUTPUT);
  pinMode(PIEZO2, OUTPUT);

  showBatteryLevel();
  delay(400);

  lastState = digitalRead(IRSENSE);
}

void loop() {
  int state = digitalRead(IRSENSE);
  if (lastState != state) {
    if (state == LOW) {  /* IR eye is active-low */
      lastVisiblePulse = millis(); 
    }
    lastState = state;
  }

  // We should be seeing falling-edge (high->low) signal changes every 10.5 ms
  if (millis() - lastVisiblePulse > 15) {
    if (state == HIGH) {
      saturated = false;
      if (! beamBroken) {
        // Emitter's been missing for a while (and this is the first
        // time we're reacting to it)
        beamBroken = true;
        /* BEAM BROKEN! WOOP WOOP WOOP DING DING! */
      }
    } else {
      // Emitter's saturating the eye. That's no good either.
      saturated = true;
    }
  } else {
    /* Reset the alarm */
    beamBroken = false;
    saturated = false;
  }

  if ((saturated) && (millis() < TESTMODE_MS)) {
    // Throw a warning (keeps firing while condition is true)
    digitalWrite(LED1, HIGH);
    digitalWrite(LED5, HIGH);
    //piezoBeep(100);
    //delay(150);
  } else {
    digitalWrite(LED1, LOW);
    digitalWrite(LED5, LOW);
  }

  if (beamBroken) {
    if (millis() < TESTMODE_MS) {
      digitalWrite(LED3, HIGH);
      //piezoBeep(500);
    }
  } else {
    digitalWrite(LED3, LOW);
  }
}

